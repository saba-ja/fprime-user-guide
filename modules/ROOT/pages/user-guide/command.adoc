= Commands

== Command Overview

Each **Component** defines a set of commands for operations. Unlike pipes, which are intended for component to component communication, commands are designed for user interaction with a component. Commands are defined through a series of properties. Users can send commands to the F' system and via `Svc::CmdDispatcher` these commands are dispatched to a handling component to invoke some behavior. The handling component handles a command by defining a command handler function to run when the command arrives.

== Command Properties

The properties defining commands are shown below:

1. `kind`: (async, sync, guarded) controls which execution context the command runs in. Sync and guarded commands run on the execution context of the command dispatcher which is in charge of sending commands to components that have functions to handle the commands. Async commands execute on the component threads that is going to handle the command.

2. Keyword `command`: is a reserved word in FPP and indicates the script is a for `command` definition.

3. `mnemonic`: a text value uniquely representing the command. The component instance name is prepended to the mnemonic to ensure uniqueness in the system.

4. `arguments`: a set of primitive and F' data types supplied to the command handler to adjust its execution. These arguments are sent from the ground.

5. `opcode`: a numerical value uniquely representing the command. This opcode is automatically adjusted to be relative to a component's base id, and thus will not collide with other commands defined by other components.

6. `priority`: For async commands, you may specify a priority. The priority specification is not allowed for other kinds of ports. The priorities regulate the order in which elements are dispatched from the message queue. Commands with higher number have higher priority.

7. Queue Full Behavior: For async commands, you may specify what should the system do when the message queue is full. There are three possible behaviors:
** assert: Fail a FSW assertion (the default behavior).
** block: Block the sender until the queue is available.
** drop: Drop the incoming message and proceed.

General format of FPP command definition

[source,fpp]
----
<async, sync, guarded> command <COMMAND_NAME> [(args, ...)] [opcode NUMBER] [priority NUMBER] [assert, block, drop]
----

Example of FPP command definition
Command with parameter a of type string. It has opcode 0x10 and priority 30 and queue full behavior of drop on queue full.

[source,fpp]
----
async command COMMAND_3(a: string) opcode 0x10 priority 30 drop
----

== Kinds of Command 

As specified above a command can be one of the following kinds: async, sync, or guarded which defines the execution context the command. Let's open up the idea in more details.

When an async command is sent by the user the command arrives on a message queue, to be dispatched on the destination component's thread (if this component is active) or on the thread of a port invocation (if this component is queued).

----
async:
[Command Dispatcher] ---Command---> [Message Queue] --Dispatch--> [Component's Thread or Port's Thread]
|                  |                |             |               |                                   |
 --- (A) ---                         --- (B) ---                    --- (C) ---

- (A) represents the entity that is sending the command asynchronously.
- (B) represents the message queue where commands are stored until they can be processed.
- (C) represents the thread of the active component or port that will handle the command.
----

When a sync command is sent by the user the command invokes a handler defined in the destination component, and run on the thread of the caller. That means that the command runs immediately on the thread of the invoking component (for example, a command dispatcher component). A synchronous command is a command that blocks the execution of other commands until it completes. Since the command runs immediately, its handler should be very short. For example, it could set a flag and then exit.

----
sync:
[Command Dispatcher] ---Command---> [Component's Handler]
|                  |                |                   |
 --- (A) ---                         --- (B) ---

- (A) represents the entity that is sending the command synchronously.
- (B) represents the component's handler which runs the command.
----

When a guarded command is sent by the user the execution behavior is similar to sync command, but the handler is guarded by a mutual exclusion lock. A mutex (short for mutual exclusion) is a synchronization primitive that ensures only one thread or task can access a guarded resource at a time. It prevents multiple executions of the same guarded command from overlapping. When a guarded command is executed, it acquires the mutex associated with it, ensuring exclusive access to the guarded resource or section of code. If another execution of the same guarded command is attempted while the mutex is held, it will wait until the mutex is released before proceeding. This mechanism helps prevent conflicts and maintain data integrity.

----
guarded:
[Command Dispatcher] --Command--> [Component's Guarded Handler] -Mutex lock-> [Execution]
|                  |              |                           |               |           |
 --- (A) ---                       --- (B) ---                                 --- (C) ---

- (A) represents the entity that is sending the command.
- (B) represents the handler in the component that is guarded by a mutex lock.
- (C) represents the execution of the command which only happens when it can acquire the mutex lock.
----

== Where to define commands

Commands are defined in the component's FPP file. In the example below the we have a project called led-blinker and it has component called Led. The commands of the component are defined in the Led.fpp file.

----
└── led-blinker
    ├── CMakeLists.txt
    ├── Components
    │   ├── CMakeLists.txt
    │   └── Led
    │       ├── CMakeLists.txt
    │       ├── Led.cpp
    │       ├── Led.fpp
    │       ├── Led.hpp
----

== Defining commands in FPP

The most basic form of a command comprises a kind (such as sync, async, guarded), followed by the keyword command, and a mnemonic serving as the command's name.

[source,fpp]
----
active component MyComp {

  async command START
  
  guarded command TOGGLE

  sync command STOP

}
----

The example above specifies the behavior of the "MyComp" component using F Prime Prime syntax. Here's the breakdown of the commands:

* `async command START`: This line declares an asynchronous command called "START". When the "START" command is executed, it will be placed in a message queue and will be executed when the target component is ready to accept the command.

* `guarded command TOGGLE`: This line declares a guarded command called "TOGGLE". This command will only run if mutex is not held by any other commands.

* `sync command STOP`: This line declares a synchronous command called "STOP". In this example when the "STOP" command is executed, it halts the execution of other commands until it finishes.

== Command Parameters

When specifying a command, you can include one or more parameters that will be sent to the spacecraft along with the command.

Specifying parameters in FPP is analogous to defining parameters for a function in a programming language. Each parameter has a name and a type, and you have the flexibility to include zero or more parameters.

In the example below, the FPP script defines an active component called SetTempComp. This component includes an asynchronous command named SET_TEMP, which is used to set a temperature value. The command accepts a parameter called temperature of type I32, allowing the user to specify the desired temperature within the range of a signed 32-bit integer.

[source,fpp]
----
active component SetTempComp {

  async command SET_TEMP(temperature: I32)

}
----

In the example below, the FPP script defines an active component named HelloWorld that includes an asynchronous command called SAY_HELLO. The command accepts a parameter named greeting, which is a string of maximum 20 characters. This parameter enables the user to provide a greeting message when executing the SAY_HELLO command.

[source,fpp]
----
active component HelloWorld {

    async command SAY_HELLO(greeting: string size 20)

}
----

You can also pass parameters with limited options. For example let's say we wanted a command that would allow the user to set state of a hardware to ON or OFF and user should not have any other option. In the example below the FPP  script defines an enumeration State with two values, OFF and ON. It also defines an active component named Switch with an asynchronous command SET_STATE, which accepts a parameter named state of type State. This component and command can be used to manipulate or control the state of a switch-like entity.

[source,fpp]
----
enum State {
  OFF
  ON
}

active component Switch {

  async command SET_STATE(state: State)

}
----

[NOTE]:The command parameters are the same as for a port parameters, except that none of the parameters may be a reference parameter. Learn more about ref parameters https://fprime-community.github.io/fpp/fpp-users-guide.html#Defining-Ports_Reference-Parameters

== Command Opcode

In an F Prime application, every command has an opcode, which is a unique numerical identifier. The opcode is used by the F Prime framework for command dispatching due to its compactness compared to the name. Command names (e.g. `mnemonic`) primarily are for human interaction.

Component opcodes are specific to each component and typically start at zero. When creating a component instance, the framework combines the base opcode of the instance with the relative opcode of the component to form global opcodes for that instance. This allows different component instances to have different opcodes for the same commands. 

If no opcode is explicitly specified for a command, FPP assigns a default opcode. The default opcode for the first command is zero, and for subsequent commands, it increments by one. 

Although relying on default opcodes is common, you can explicitly specify opcodes using the "opcode" keyword followed by a numeric expression.

In the following example FPP script defines a component called CommandOpcodes. It includes three commands: COMMAND_1, COMMAND_2, and COMMAND_3.

* COMMAND_1 is an asynchronous command with a default opcode of 0x0.
* COMMAND_2 is an asynchronous command with two parameters, 'a' of type F32 and 'b' of type U32. It has an explicit opcode of 0x10.
* COMMAND_3 is a synchronous command with a default opcode of 0x11.

[source,fpp]
----
active component CommandOpcodes {

  async command COMMAND_1

  async command COMMAND_2(a: F32, b: U32) opcode 0x10

  sync command COMMAND_3

}
----

Within a component, the command opcodes must be unique. For example, this component is incorrect because the opcode zero appears twice:

In the example below The FPP script defines a component called DuplicateOpcode. It includes two commands: COMMAND_1 and COMMAND_2.

* COMMAND_1 is an asynchronous command with an opcode of 0x0.
* COMMAND_2 is also an asynchronous command, but it mistakenly has the same opcode (0x0) as COMMAND_1.

[source,fpp]
----
active component DuplicateOpcode {

  async command COMMAND_1

  async command COMMAND_2 opcode 0x0

}
----

== Command Priority

When specifying an async command, you may specify a priority for the command execution. Command priority in a message queue refers to the relative importance or urgency assigned to different commands in the queue. It determines the order in which commands are processed, with higher priority commands being executed before lower priority ones. You can put the priority number after the command name and after the parameters and opcode, if any. 

Here is an example:

[source,fpp]
----
active component MyPriorityComp {

  async command COMMAND_1 priority 10

  async command COMMAND_2(a: U32, b: F32) priority 20

  async command COMMAND_3(a: string) opcode 0x10 priority 30

}
----

In the example above the FPP script defines an active component called MyPriorityComp. It includes the declaration of three asynchronous commands. The first command, named COMMAND_1, is declared with a priority of 10. The second command, named COMMAND_2, has formal parameters 'a' of type U32 and 'b' of type F32. It is assigned a priority of 20. Lastly, the script declares an asynchronous command named COMMAND_3. It has a formal parameter 'a' of type string, an opcode of 0x10, and a priority of 30.

[NOTE]: Command priority is only supported for async commands.

== Command Queue Full Behavior

As we specified above async commands are placed in a message queue first then they will be dispatched to the their destination component when the component is ready to handle the command. Now the question is what should happen when the message queue is full. By default, when an async command invocation leads to a message queue overflow, it triggers a assertion failure. The developer can capture the failure assertion and decide what to do next. The usual behavior is to abort the Flight Software and perform a system reset.
You may change the Queue Full behavior in FPP by specifying what should the system do when the message queue is full. 

There are three possible behaviors for message queue full:
** assert: Fail a FSW assertion (the default behavior).
** block: Block the sender until the queue is available.
** drop: Drop the incoming message and proceed.

Here is an example:

[source,fpp]
----
active component MyQueueFullComp {

  async command COMMAND_1

  async command COMMAND_2(a: U32, b: F32) block

  async command COMMAND_3(a: string) opcode 0x10 priority 30 drop

}
----

The provided script defines an active component called "MyQueueFullComp" with three async commands: COMMAND_1, COMMAND_2, and COMMAND_3. COMMAND_1 has a default Queue Full behavior which means it will fail a software assertion when the queue is full. COMMAND_2 is set to block the sender until the message queue is available, while COMMAND_3 is configured to drop incoming messages when the queue is full.

== Command Annotations

It is a good idea to add annotation to commands to improve readability of FPP and the autogenerated code.
As a reminder FPP annotation refers to a feature used to provide additional information or metadata about the code written in FPP. Annotations are used to add comments, instructions, or documentation to the code to improve its readability, maintainability, and understanding. Annotations in FPP start with the '@' symbol. The annotation will be converted comments after autogenerate the F Prime code. To learn more about FPP annotation see https://fprime-community.github.io/fpp/fpp-users-guide.html#Writing-Comments-and-Annotations_Annotations

Here is an example:

[source,fpp]
----
@ Component to blink an LED driven by a rate group
  active component Led {

    # ----------------------------------------------------------------------
    # Commands
    # ----------------------------------------------------------------------

    @ Command to turn on or off the blinking LED
    async command BLINKING_ON_OFF(
            on_off: Fw.On @< Indicates whether the blinking should be on or off
    )

  }
----

The autogenerated code will look like the following:

[source,cpp]
----
    //! Command to turn on or off the blinking LED
    void BLINKING_ON_OFF_cmdHandler(
      const FwOpcodeType opCode, 
      const U32 cmdSeq,          
      Fw::On on_off   /*!< Indicates whether the blinking should be on or off */
    );
----

[INFO] The # is FPP comment and will be ignored by the FPP parser engine. On the hand @ is to an annotation and text in front of it will be converted to CPP comments in the autogenerated code.

== Command Dispatching

When commands are defined for a component, the autocoder automatically includes ports for registering and receiving commands, as well as reporting the execution status once finished. For each component that handles commands, it should be connected to the command dispatcher. This connection involves linking the registration, dispatch, and response ports in parallel.

image::cmd_dispatcher_diagram.drawio.png[Command dispatcher model]

**Figure Command dispatcher.** The command dispatcher receives the raw buffer containing the command and arguments.
The command opcode is extracted, and a lookup table is used to find the handling component. The argument buffer is then
passed to the component, and the command dispatcher waits without blocking for the component to return status.

As shown in the diagram above any component that handles commands need to create a port for registering commands with the dispatcher, one for receiving commands from the dispatcher, one for sending out the command response to the dispatcher. 

== Command Sequencing

In many projects, commands need to be sequenced in order. In order to facilitate this, the framework provides
`Svc::CmdSequencer`. The command sequencer reads a defined sequence of commands and sends each in turn to the command
dispatcher to be dispatched and the command execution status is returned to the sequencer.  This is an alternate path to send command buffers to the command dispatcher than the external path from ground.

![Command Sequencer](../media/data_model2.png)

**Figure 5. Command sequence.** The command sequencer loads a sequence file from the file system, sends the command,
 and waits for the response for each command in the sequence. A failed response terminates the sequence,
while a successful response moves to the next command in the sequence.