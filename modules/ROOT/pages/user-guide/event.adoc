= Event

== What is an F Prime Event

Events represent a log of activities taken by the embedded system. Events can be thought of in the same way as a program execution log in that they enable the ability to trace the execution of the system. Events are sent out of the system via the Svc::ActiveLogger component and components defining commands should hook up the log port to it. If console logging is desired, the text log port can be hooked up to the Svc::PassiveConsoleTextLogger component. Events are defined per component and are typically used to capture what the component is doing. Events can be sporadic; however, should all be captured for downlink.

When defining an F Prime component, you may specify one or more events. The F Prime framework converts each event into a C++ function that you can call from the component implementation. Calling the function emits a serialized event report that you can store in an on-board file system or send to the ground.

== Event properties

Events are defined by the following properties:


* id: a numeric id uniquely define this event. It is automatically offset by the component's base id to ensure global uniqueness.
* name: a unique text identifier for this event. This is prepended by the component instance name to remain unique.
* severity: a text identifier identifying the severity of this event. Possible values are:
** DIAGNOSTIC: akin to debug messages. Usually not sent to the ground.
** ACTIVITY_LO: akin to fine info messages these typically come from background tasks
** ACTIVITY_HI: akin to info messages these typically come from foreground tasks
** WARNING_LO: less severe warning events
** WARNING_HI: high-severity warning events, although the system can still function
** FATAL: fatal events indicating that the system must reboot
** COMMAND: events tracing the execution of commands
* arguments: like command arguments, these are primitive and complex types that represent the variable data associated with the event. These are injected into the format string for a full text representation of the event.
* format string: a C-style format string used to reconstruct a text version of the event.

NOTE: The use of events to severity levels are based on the judgement of the system designer.

Code in the component-specific generated base class provides a function to call to emit each event defined by the component. This function expects an argument to be supplied for each argument defined by the event. The code generator automatically adds ports for retrieving a time tag and sending events. There are two independent ports for sending events:

* Binary log output port for sending outside the system
* A text log output port for on-board consoles

== Event Logging

Events first acquire a time tag to represent when they occurred and then are typically sent to the Svc::ActiveLogger component on their way to be sent down to the ground. This logger component both processes the event and also recognizes and begins responses for FATAL severity events.

image::data_model3.png[Event log]

Figure 6. Event log. The component implementation calls a function to generate the event. The base class retrieves the time tag from the time source component. The component sends the event to the event log component, which reads it from the port queue and sends it to the ground.

== Basic Events

The simplest event consists of the keyword `event`, a name, a severity,
and a format string.
The name is the name of the event.
A severity is the keyword `severity` and one of the following:

* `activity` `high`: Spacecraft activity of greater importance.

* `activity` `low`: Spacecraft activity of lesser importance.

* `command`: An event related to commanding.
Primarily used by the command dispatcher.

* `diagnostic`: An event relating to system diagnosis
and debugging.

* `fatal`: An event that causes the system to abort.

* `warning` `high`: A warning of greater importance.

* `warning` `low`: A warning of lesser importance.

A format is the keyword `format` and a literal string for
use in a formatted real-time display or event log.

As an example, here is an active component called `BasicEvents`
with a few basic events.

[source,fpp]
----
@ A component for illustrating basic events
passive component BasicEvents {

  # ----------------------------------------------------------------------
  # Ports
  # ----------------------------------------------------------------------

  @ Event port
  event port eventOut

  @ Text event port
  text event port textEventOut

  @ Time get port
  time get port timeGetOut

  # ----------------------------------------------------------------------
  # Events
  # ----------------------------------------------------------------------

  @ Activity low event
  event Event1 severity activity low format "Event 1 occurred"

  @ Warning low event
  event Event2 severity warning low format "Event 2 occurred"

  @ Warning high event
  event Event3 severity warning high format "Event 3 occurred"

}
----

Notice that we defined the two
<<Defining-Components_Special-Port-Instances_Event-Ports,
event ports>>
and a
<<Defining-Components_Special-Port-Instances_Time-Get-Ports,
time get port>>
for this component.
All three ports are required for any component that has events.
As an example, try deleting one or more of these ports from the
code above and running the result through `fpp-check`.

== Formal Parameters

When specifying an event, you may specify one or more
formal parameters.
The parameters are bound to arguments when the component
instance emits the event.
The argument values appear in the formatted text
that describes the event.

The formal parameters of an event are the same
as for a <<Defining-Ports_Formal-Parameters,port definition>>, except
that none of the parameters may be a
<<Defining-Ports_Reference-Parameters,reference parameter>>.
For each formal parameter, there must be a corresponding
replacement field in the format string.
The replacement fields for event format strings are the same as for
format strings in
<<Defining-Types_Array-Type-Definitions_Format-Strings,
type definitions>>.
The replacement fields in the format string match the event
parameters, one for one and in the same order.

As an example, here is a component with two events,
each of which has formal parameters.
Notice how the replacement fields in the event format
strings correspond to the formal parameters.

[source,fpp]
----
@ An enumeration of cases
enum Case { A, B, C }

@ An array of 3 F64 values
array F64x3 = [3] F64

@ A component for illustrating event formal parameters
passive component EventParameters {

  # ----------------------------------------------------------------------
  # Ports
  # ----------------------------------------------------------------------

  @ Event port
  event port eventOut

  @ Text event port
  text event port textEventOut

  @ Time get port
  time get port timeGetOut

  # ----------------------------------------------------------------------
  # Events
  # ----------------------------------------------------------------------

  @ Event 1
  @ Sample output: "Event 1 occurred with argument 42"
  event Event1(
    arg1: U32 @< Argument 1
  ) \
    severity activity high \
    format "Event 1 occurred with argument {}"

  @ Event 2
  @ Sample output: "Saw value [ 0.001, 0.002, 0.003 ] for case A"
  event Event2(
    case: Case @< The case
    value: F64x3 @< The value
  ) \
    severity warning low \
    format "Saw value {} for case {}"

}
----

== Identifiers

Every event in an F Prime FSW application has a unique
numeric identifier.
As for
<<Defining-Components_Commands_Opcodes,command opcodes>>,
the event identifiers for a component are specified
relative to the component, usually starting from
zero and counting up by one.
If you omit the identifier, then
FPP assigns a default identifier: zero for the first
event in the component; otherwise one more than the
identifier of the previous event.

If you wish, you may explicitly specify one or more event
identifiers.
To do this, you write the keyword `id` followed
by a numeric expression immediately before the keyword `format`.
Here is an example:

[source,fpp]
----
@ Component for illustrating event identifiers
passive component EventIdentifiers {

  # ----------------------------------------------------------------------
  # Ports
  # ----------------------------------------------------------------------

  @ Event port
  event port eventOut

  @ Text event port
  text event port textEventOut

  @ Time get port
  time get port timeGetOut

  # ----------------------------------------------------------------------
  # Events
  # ----------------------------------------------------------------------

  @ Event 1
  @ Its identifier is 0x00
  event Event1 severity activity low \
    id 0x10 \
    format "Event 1 occurred"

  @ Event 2
  @ Its identifier is 0x10
  event Event2(
    count: U32 @< The count
  ) \
    severity activity high \
    id 0x11 \
    format "The count is {}"

  @ Event 3
  @ Its identifier is 0x11
  event Event3 severity activity high \
    format "Event 3 occurred"

}
----

Within a component, the event identifiers must be unique.

== Throttling

Sometimes it is necessary to throttle events, to ensure that
they do not flood the system.
For example, suppose that the FSW requests some resource _R_
at a rate _r_ of several times per second.
Suppose further that if _R_ is unavailable, then the FSW
emits a warning event.
In this case, we typically do not want the FSW to emit an unbounded number
of warnings at rate _r_; instead, we want it to emit a single warning
or a few warnings.

To achieve this behavior, you can write the keyword `throttle` and a
numeric expression after the format string.
The expression must evaluate to a constant value _n_.
After an instance of the component has emitted the event _n_ times, it will
stop emitting the event.
Here is an example:

[source,fpp]
----
@ Component for illustrating event throttling
passive component EventThrottling {

  # ----------------------------------------------------------------------
  # Ports
  # ----------------------------------------------------------------------

  @ Event port
  event port eventOut

  @ Text event port
  text event port textEventOut

  @ Time get port
  time get port timeGetOut

  # ----------------------------------------------------------------------
  # Events
  # ----------------------------------------------------------------------

  @ Event 1
  event Event1 severity warning high \
    format "Event 1 occurred" \
    throttle 10

}
----

In this example, event `E` will be throttled after the component
instance has emitted it ten times.

Once an event is throttled, the component instance will no longer
emit the event until the throttling is canceled.
Typically, the canceling happens via a FSW command.
For details, see the
https://github.com/nasa/fprime/blob/master/docs/UsersGuide/guide.md[F Prime User's Guide].



